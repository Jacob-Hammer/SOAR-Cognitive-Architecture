##!
# @file
#
# @created jhammer4 20210304

rl -s learning on

sp {propose*addAnimals
    (state <s> ^type state)
    (<s> -^locations)
-->
    (<s> ^operator <o> !)
    (<o> ^name addAnimals)
}

sp {apply*addAnimals
    (state <s> ^operator <o>)
    (<o> ^name addAnimals)
-->
    (<s> ^locations <loc>)
    (<loc> ^side1 <side1>)
    (<loc> ^side2 <side2>)
    (<side1> ^item rabbit)
    (<side1> ^item lettuce)
    (<side1> ^item fox)
    (<side1> ^other side2)
    (<side2> ^other side1)
    (<s> ^location side1)
}

sp {elaborate*animals
    (state <s> ^type state)
    (<s> ^location <here>)
    (<s> ^locations <loc>)
    (<loc> ^<here> <side>)
    (<side> ^item <item>)
-->
    (write (crlf) | item |<item>)
}

sp {elaborate*location
    (state <s> ^type state)
    (<s> ^location <here>)
    (<s> ^locations <loc>)
    (<loc> ^<here> <side>)
    (<side> ^other <other>)
-->
    (write (crlf) | location |<here>)
}

sp {propose*cross_alone
    (state <s> ^type state)
    (<s> ^location <here>)
    (<s> ^locations <loc>)
    (<loc> ^<here> <side>)
    (<side> ^other <there>)
-->
    (<s> ^operator <o> +)
    (<o> ^name cross_alone)
    (<o> ^here <here>)
    (<o> ^there <there>)
}

sp {apply*cross_alone
    (state <s> ^operator <o>)
    (<o> ^name cross_alone)
    (<o> ^here <here>)
    (<o> ^there <there>)
-->
    (write(crlf) |Crossing from |<here>| to |<there>)
    (<s> ^location <here> -)
    (<s> ^location <there>)
}

sp {propose*cross_lettuce
    (state <s> ^type state)
    (<s> ^location <here>)
    (<s> ^locations <loc>)
    (<loc> ^<here> <side>)
    (<side> ^other <there>)
    (<loc> ^<there> <otherside>)
    (<side>^item lettuce)
-->
    (<s> ^operator <o> +)
    (<o> ^name cross_lettuce)
    (<o> ^to_remove lettuce)
    (<o> ^otherside <otherside>)
    (<o> ^side <side>)
    (<o> ^here <here>)
    (<o> ^there <there>)
}

sp {apply*cross_lettuce
    (state <s> ^operator <o>)
    (<o> ^name cross_lettuce)
    (<o> ^here <here>)
    (<o> ^there <there>)
    (<o> ^side <side>)
    (<o> ^otherside <otherside>)
    (<o> ^to_remove lettuce)
-->
    (<side> ^item lettuce -)
    (<otherside> ^item lettuce)
    (write (crlf) |>> crossing from |<here>| to |<there>| with lettuce|)
    (<s> ^location <here> -)
    (<s> ^location <there>)
}

sp {propose*cross_rabbit
    (state <s> ^type state)
    (<s> ^location <here>)
    (<s> ^locations <loc>)
    (<loc> ^<here> <side>)
    (<side> ^other <there>)
    (<loc> ^<there> <otherside>)
    (<side>^item rabbit)
-->
    (<s> ^operator <o> +)
    (<o> ^name cross_rabbit)
    (<o> ^to_remove rabbit)
    (<o> ^otherside <otherside>)
    (<o> ^side <side>)
    (<o> ^here <here>)
    (<o> ^there <there>)
}

sp {apply*cross_rabbit
    (state <s> ^operator <o>)
    (<o> ^name cross_rabbit)
    (<o> ^here <here>)
    (<o> ^there <there>)
    (<o> ^side <side>)
    (<o> ^otherside <otherside>)
    (<o> ^to_remove rabbit)
-->
    (<side> ^item rabbit -)
    (<otherside> ^item rabbit)
    (write(crlf) |>> crossing from |<here>| to |<there>| with rabbit|)
    (<s> ^location <here> -)
    (<s> ^location <there>)
}

sp {propose*cross_fox
    (state <s> ^type state)
    (<s> ^location <here>)
    (<s> ^locations <loc>)
    (<loc> ^<here> <side>)
    (<side> ^other <there>)
    (<loc> ^<there> <otherside>)
    (<side>^item fox)
-->
    (<s> ^operator <o> +)
    (<o> ^name cross_fox)
    (<o> ^to_remove fox)
    (<o> ^otherside <otherside>)
    (<o> ^side <side>)
    (<o> ^here <here>)
    (<o> ^there <there>)
}

sp {apply*cross_fox
    (state <s> ^operator <o>)
    (<o> ^name cross_fox)
    (<o> ^here <here>)
    (<o> ^there <there>)
    (<o> ^side <side>)
    (<o> ^otherside <otherside>)
    (<o> ^to_remove fox)
-->
    (<side> ^item fox -)
    (<otherside> ^item fox)
    (write (crlf) |>> crossing from |<here>| to |<there>| with fox|)
    (<s> ^location <here> -)
    (<s> ^location <there>)
}

sp {detect*lettuce_eaten
    (state <s> ^type state
               ^reward-link <r1>
               ^location <here>
               ^locations <loc>)
    (<loc> ^<here> <side>)
    (<side> -^item lettuce
            -^item rabbit)
-->
    (write (crlf) | >>> Rabbit ate the lettuce!! <<<|)
    (<r1> ^reward <r2>)
    (<r2> ^value -1)
    (halt)
}

sp {detect*rabbit_eaten
    (state <s> ^type state
               ^reward-link <r1>
               ^location <here>
               ^locations <loc>)
    (<loc> ^<here> <side>)
    (<side> -^item rabbit
            -^item fox)
-->
    (write (crlf) | >>> Fox ate the Rabbit|)
    (<r1> ^reward <r2>)
    (<r2> ^value -1)
    (halt)
}

sp {detect*success
    (state <s> ^type state
               ^reward-link <r1>
               ^location side2
               ^locations <loc>)
    (<loc> ^side2 <side2>)
    (<side2> ^item rabbit)
    (<side2> ^item fox)
    (<side2> ^item lettuce)
-->
    (<r1> ^reward <r2>)
    (<r2> ^value 1)
    (write (crlf) |>>>>>>>>>>>> All across safely <<<<<<<<<<<<<<|)
#    (write (crlf) | I'm on | <side2>)
    (halt)
}


#############################################

sp {rl*rabbit-lettuce-fox*cross_alone
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_alone)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            ^item lettuce
            ^item fox)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*rabbit-lettuce-fox*cross_rabbit
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_rabbit)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            ^item lettuce
            ^item fox)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*rabbit-lettuce-fox*cross_lettuce
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_lettuce)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            ^item lettuce
            ^item fox)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*rabbit-lettuce-fox*cross_fox
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_fox)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            ^item lettuce
            ^item fox)
-->
    (<s> ^operator <o> = 0)
}

#####
sp {rl*rabbit-lettuce*cross_alone
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_alone)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            ^item lettuce
            -^item fox)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*rabbit-lettuce*cross_rabbit
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_rabbit)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            ^item lettuce
            -^item fox)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*rabbit-lettuce*cross_lettuce
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_lettuce)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            ^item lettuce
            -^item fox)
-->
    (<s> ^operator <o> = 0)
}

#####

sp {rl*lettuce-fox*cross_alone
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_alone)
    (<loc> ^<here> <side>)
    (<side> -^item rabbit
            ^item lettuce
            ^item fox)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*lettuce-fox*cross_lettuce
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_lettuce)
    (<loc> ^<here> <side>)
    (<side> -^item rabbit
            ^item lettuce
            ^item fox)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*lettuce-fox*cross_fox
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_fox)
    (<loc> ^<here> <side>)
    (<side> -^item rabbit
            ^item lettuce
            ^item fox)
-->
    (<s> ^operator <o> = 0)
}
#####

sp {rl*rabbit-fox*cross_alone
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_alone)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            -^item lettuce
            ^item fox)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*rabbit-fox*cross_rabbit
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_rabbit)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            -^item lettuce
            ^item fox)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*rabbit-fox*cross_fox
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_fox)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            -^item lettuce
            ^item fox)
-->
    (<s> ^operator <o> = 0)
}

#####

sp {rl*rabbit*cross_alone
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_alone)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            -^item lettuce
            -^item fox)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*rabbit*cross_rabbit
    (state <s> ^type state
               ^location <here>
               ^locations <loc>
               ^operator <o> +)
    (<o> ^name cross_rabbit)
    (<loc> ^<here> <side>)
    (<side> ^item rabbit
            -^item lettuce
            -^item fox)
-->
    (<s> ^operator <o> = 0)
}
