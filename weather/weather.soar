
## PROPOSAL RULES

# Each rule has a condition and application section. Each rule also has a name. 
# It is a common convention to start your rules with either 'propose' or 'apply', 
#   then '*' then the name of the rule.
sp {propose*ItsRaining
    # Our first condition always must start with 'state' and match to something on it. 
    # Here, we matched to the attribute/value pair of type/state. 
    # This tells soar to find a state with that attribute/value pair. 
    # Only one exists, so it matches to that. You'll find that this line of code usually stays the same, 
    #   at least until you get into more advanced topics. 
    # You almost always want to match to the top state in proposal rules so we can transverse below it.
    (state <s> ^type state)
    
    # Next, we transverse the tree to get all the way down to the input-link to match our three values. 
    # Convince yourself that that each of these rules would fire if the weather/(Sunny, cloudy, or rainy)
    #   pair were on the input-link by comparing the WM graphs at the top of this page to the Conditional Groups 
    #   that soar would create with these conditions.
    (<s> ^io <io>)
    (<io> ^input-link <input>)
    (<input> ^weather raining)
-->
    
    # From the same state that matched in condition one, we created a new operator Identifier 
    #   and create a new identifier (named <o>). Ignore the '+ =' for now. 
    (<s> ^operator <o> + =)
    
    # Next, using <o>, we add two attribute/value pairs: name/ItsSnowing and value/snowy. 
    # It is best practice to give every operator a 'name' with the same name as the rule name.
    (<o> ^name ItsRaining)
}
 
 
sp {propose*ItsSunny
    (state <s> ^type state)
    (<s> ^io <io>)
    (<io> ^input-link <input>)
    (<input> ^weather sunny)
-->
    (<s> ^operator <o> + =)
    (<o> ^name ItsSunny)
}
 
 
sp {propose*ItsSnowing
    (state <s> ^type state)
    (<s> ^io <io>)
    (<io> ^input-link <input>)
    (<input> ^weather Snowing)
-->
    (<s> ^operator <o> + =)
    (<o> ^name ItsSnowing)
}


## APPLICATION RULES

# Notice how we changed the named from propose*name to apply*name.
sp {apply*ItsRaining
    # Instead of matching to type/state like in our proposal rules, we matched to an operator. 
    # From the matched operator, we then matched to name/(itsRaining/ItsSunny/ItsSnowing). 
    # Each one of these rules will only run for its given proposal rule/operator, 
    #   since the Conditional Graph matches to the the operator name specifically. 
    (state <s> ^operator <o>)
    (<o> ^name ItsRaining)
-->
    # Now, we'll describe what happens at each step with the above rules. 
    # For this example, let's say that the attribute/value pair on the input-link is: weather/snow 
    (write |It's raining|)
}
 
sp {apply*ItsSunny
    (state <s> ^operator <o>)
    (<o> ^name ItsSunny)
-->
    (write |It's sunny|)
}
 
 
 
sp {apply*ItsSnowing
    (state <s> ^operator <o>)
    (<o> ^name ItsSnowing)
-->
    (write |It's snowing|)
}