##!
# @file
#
# @created jhammer4 20210205

#smem --enable
#epmem --set learning on

sp {propose*init
  (state <s> ^type state)
  (<s> ^superstate nil)
  (<s> -^init)
-->
  (<s> ^operator <o> + !)
  (<o> ^name init)
}

sp {apply*init
  (state <s> ^operator <o>)
  (<o> ^name init)
  (<s> ^smem.command <cmd>)
-->
  (<food> ^type food)
  (<food> ^cake 500)
  (<food> ^banana 50)
  (<food> ^soda 200)
  (<food> ^chicken 300)
  (<s> ^init true)
  (<cmd> ^store <food>)
  (<s> ^fridge <fri>)
  (<fri> ^food soda)
  (<fri> ^food cake)
  (<fri> ^food banana)
}

sp {propose*eatFood
  (state <s> ^type state)
  (<s> ^fridge.food <food>)
-->
  (<s> ^operator <o> +)
  (<o> ^name <food>)
}
 
sp {apply*eatFood
  (state <s> ^operator <o>)
  (<o> ^name <food>)
  (<s> ^fridge <fri>)
  (<fri> ^food <food>)
-->
  (<fri> ^food <food> -)
  (<s> ^selected <sel>)
  (<sel> ^food <food>)
  (write |Eating |<food>(crlf))
}

sp {propose*clearSelected
  (state <s> ^type state)
  (<s> ^selected <sel>)
  (<sel> ^food <food>)
-->
  (<s> ^operator <o> !)
  (<o> ^name clearSelected)
}
 
sp {apply*clearSelected
  (state <s> ^operator <o>)
  (<o> ^name clearSelected)
  (<s> ^selected <sel>)
  (<sel> ^food <food>)
-->
  (<sel> ^food <food> -)
  (<s> ^selected <sel> -)
}
 
sp {propose*getfirstMemory
  (state <s> ^type state)
  (<s> ^fridge <fri>)
  (<fri> -^food)
  (<s> -^gotfirstmem)
  (<s> -^remembered)
  (<s> ^epmem.command <cmd>)
-->
  (<s> ^operator <o> +)
  (<o> ^name getfirstMem)
}

sp {apply*getfirstMemory
  (state <s> ^operator <o>)
  (<o> ^name getfirstMem)
  (<s> ^epmem.command <cmd>)
-->
  (<s> ^gotfirstmem true)
  (<cmd> ^retrieve 1)
}

sp {propose*cleanRetrieve
  (state <s> ^type state)
  (<s> -^remembered)
  (<s> ^epmem.command <cmd>)
  (<cmd> ^retrieve <r>)
-->
  (<s> ^operator <o> +)
  (<o> ^name cleanRetrieve)
}

sp {apply*cleanRetrieve
  (state <s> ^operator <o>)
  (<o> ^name cleanRetrieve)
  (<s> ^epmem.command <cmd>)
  (<cmd> ^retrieve <r>)
-->
  (<cmd> ^retrieve <r> -)
}

sp {propose*getnextMemory
  (state <s> ^type state)
  (<s> -^remembered)
  (<s> ^gotfirstmem true)
  (<s> -^gotnext)
  (<s> ^epmem.command <cmd>)
  (<cmd> -^retrieve)
-->
  (<s> ^operator <o> +)
  (<o> ^name getnextMemory)
}
 
sp {apply*getnextMemory
  (state <s> ^operator <o>)
  (<o> ^name getnextMemory)
  (<s> -^remembered)
  (<s> ^epmem.command <cmd>)
-->
  (<cmd> ^next <n>)
  (<s> ^gotnext true)
}
 
sp {propose*notremembered
  (state <s> ^type state)
  (<s> -^remembered)
  (<s> ^gotnext true)
  (<s> ^epmem.result <result>)
  (<result> ^retrieved <retrieved>)
  (<retrieved> -^selected)
-->
  (<s> ^operator <o> +)
  (<o> ^name notremembered)
}
 
sp {apply*notremembered
  (state <s> ^operator <o>)
  (<o> ^name notremembered)
  (<s> ^epmem.command <cmd>)
  (<cmd> ^next <n>)
-->
  (<cmd> ^next <n> -)
  (<s> ^gotnext true -)
}
 
sp {propose*remembered
  (state <s> ^type state)
  (<s> -^remembered)
  (<s> ^epmem.result <result>)
  (<result> ^retrieved <retrieved>)
  (<retrieved> ^selected.food <food>)
-->
  (<s> ^operator <o> +)
  (<o> ^name remembered)
  (<o> ^food <food>)
}
 
sp {apply*remembered
  (state <s> ^operator <o>)
  (<o> ^name remembered)
  (<o> ^food <food>)
-->
  (<s> ^remembered true)
  (write | first selected food | <food>)
}
 
sp {detect*remembered
  (state <s> ^type state)
  (<s> ^remembered true)
-->
  (halt)
}

sp {elaboration*Substate*queryCalories
  (state <s> ^impasse tie) 
  (<s> ^smem.command <cmd>)
-->
  (<cmd> ^query.type <food>)
}
 
sp {propose*Substate*breakImpasse
  (state <s> ^impasse tie)
  (<s> ^item.name <food1>)
  (<s> ^item.name <food2>)
  (<s> ^smem.result.retrieved.<food1> <foodc1>)
  (<s> ^smem.result.retrieved.<food2> <foodc2>)
  (<s> ^smem.result.retrieved.<food1> <foodc1> < <foodc2>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name breakImpasse)
  (<o> ^food1 <food1>)
  (<o> ^food2 <food2>)
}
 
sp {apply*Substate*breakImpasse
  (state <s> ^operator <o>)
  (<o> ^name breakImpasse)
  (<o> ^food1 <food1>)
  (<o> ^food2 <food2>)
  (<s> ^item.name <food1>)
  (<s> ^item.name <food2>)
  (<s> ^superstate <ss>)
  (<ss> ^operator <os1> +)
  (<ss> ^operator <os2> +)
  (<os1> ^name <food1>)
  (<os2> ^name <food2>)
-->
  (<ss> ^operator <os1> > <os2>)
}
